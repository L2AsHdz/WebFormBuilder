package com.l2ashdz.appcliente.analizadores.sintactico;

import com.l2ashdz.appcliente.model.Token;
import com.l2ashdz.appcliente.model.errores.ErrorAnalisis;
import com.l2ashdz.appcliente.model.errores.TipoError;
import com.l2ashdz.appcliente.model.solicitudes.Solicitud;
import com.l2ashdz.appcliente.model.solicitudes.TipoSolicitud;
import com.l2ashdz.appcliente.model.solicitudes.Parametro;

import java.util.ArrayList;
import java.util.List;

import java_cup.runtime.Symbol;

parser code{:

    private List<ErrorAnalisis> errores = new ArrayList();
    private List<Solicitud> solicitudes = new ArrayList();
    private List<Parametro> parametros = new ArrayList();

    public List<ErrorAnalisis> getErrores() {
        return this.errores;
    }

    public void syntax_error(Symbol s) {
        Token t = (Token) s.value;
        StringBuilder descripcion = new StringBuilder("Se esperaba: ");
        expected_token_ids().forEach(x -> descripcion.append(symbl_name_from_id(x)).append(", "));
        errores.add(new ErrorAnalisis(t.getLexema(), t.getLinea(), t.getColumna(), TipoError.SINTACTICO, descripcion.toString()));
    }

    public void unrecovered_syntax_error(Symbol cur_token){
        Token t = (Token) cur_token.value;
        System.out.println("Error irrecuperable " + t.getLexema());
    }

    protected int error_sync_size() {
        return 1;
    }
:}

//Terminales
terminal Token      START_REQUEST, END_REQUEST, USER_CREDENTIALS;
terminal Token      LOGIN, CREATE_USER, DELETE_USER, MODIFY_USER;
terminal Token      PARAM_USUARIO, PARAM_PASSWORD, PARAM_FECHA_CREACION, PARAM_FECHA_MODIFICACION, PARAM_OLD_USER, PARAM_NEW_USER, PARAM_NEW_PASSWORD;
terminal Token      LESS_THAN, GREATER_THAN, EXCLAMATION_MARK, QUOTE_MARK, COLON, COMMA, OPEN_BRACE, CLOSE_BRACE, OPEN_BRACKET, CLOSE_BRACKET;
terminal Token      VALUE;

//no terminales
non terminal        startRequest, requestType, startContent, blockParamName, endContent, endRequest;
non terminal        startParamsLogin, startParamsCreateUser, startParamsModifyUser, startParamsDeleteUser;
non terminal        endParamsLogin, endParamsCreateUser, endParamsModifyUser, endParamsDeleteUser;

non terminal List<Parametro>    paramsLogin, paramsCreateUser, paramsModifyUser;
non terminal Parametro          paramLogin, paramCreateUser, paramModifyUser, paramDeleteUser;
non terminal String             nameParamLogin, nameParamCreateUser, nameParamModifyUser, value;


/******************************Produccion para el inicio de una solicitud en general******************************/
startRequest ::= LESS_THAN EXCLAMATION_MARK START_REQUEST COLON QUOTE_MARK requestType endContent endRequest;

/******************************Producciones para los diferentes tipos de solicitud******************************/
requestType ::= LOGIN QUOTE_MARK GREATER_THAN startContent startParamsLogin
| CREATE_USER QUOTE_MARK GREATER_THAN startContent startParamsCreateUser
| MODIFY_USER QUOTE_MARK GREATER_THAN startContent startParamsModifyUser
| DELETE_USER QUOTE_MARK GREATER_THAN startContent startParamsDeleteUser
;

startContent ::= OPEN_BRACE QUOTE_MARK blockParamName QUOTE_MARK COLON OPEN_BRACKET;

blockParamName ::= USER_CREDENTIALS;

/******************************Producciones para parametros de solicitud Login******************************/
startParamsLogin ::= OPEN_BRACE paramsLogin endParamsLogin;

paramsLogin ::= paramLogin COMMA paramsLogin
              | paramLogin
              ;

paramLogin ::= QUOTE_MARK nameParamLogin QUOTE_MARK COLON QUOTE_MARK value QUOTE_MARK;

nameParamLogin ::= PARAM_USUARIO:p          {:RESULT = p.getLexema();:}
                 | PARAM_PASSWORD:p         {:RESULT = p.getLexema();:}
                 ;
                 
endParamsLogin ::= CLOSE_BRACE COMMA startParamsLogin
                 | CLOSE_BRACE
                 ;
/**************************Fin de producciones para parametros de solicitud Login**************************/    



/******************************Producciones para parametros de solicitud CrearUsuario******************************/
startParamsCreateUser ::= OPEN_BRACE paramsCreateUser endParamsCreateUser;

paramsCreateUser ::= paramCreateUser COMMA paramsCreateUser
                   | paramCreateUser
                   ;
     
paramCreateUser ::= QUOTE_MARK nameParamCreateUser QUOTE_MARK COLON QUOTE_MARK value QUOTE_MARK;

nameParamCreateUser ::= PARAM_USUARIO:p               {:RESULT = p.getLexema();:}
                      | PARAM_PASSWORD:p              {:RESULT = p.getLexema();:}
                      | PARAM_FECHA_CREACION:p        {:RESULT = p.getLexema();:}
                      ;
           
endParamsCreateUser ::= CLOSE_BRACE COMMA startParamsCreateUser
                      | CLOSE_BRACE
                      ;
/****************************Fin producciones para parametros de solicitud CrearUsuario****************************/      



/**************************Producciones para parametros de solicitud ModificarUsuario*************************/
startParamsModifyUser ::= OPEN_BRACE paramsModifyUser endParamsModifyUser;

paramsModifyUser ::= paramModifyUser COMMA paramsModifyUser
                   | paramModifyUser
                   ;

paramModifyUser ::= QUOTE_MARK nameParamModifyUser QUOTE_MARK COLON QUOTE_MARK value QUOTE_MARK;

nameParamModifyUser ::= PARAM_OLD_USER:p                  {:RESULT = p.getLexema();:}
                      | PARAM_NEW_USER:p                  {:RESULT = p.getLexema();:}
                      | PARAM_NEW_PASSWORD:p              {:RESULT = p.getLexema();:}
                      | PARAM_FECHA_MODIFICACION:p        {:RESULT = p.getLexema();:}
                      ;

endParamsModifyUser ::= CLOSE_BRACE COMMA startParamsModifyUser
                      | CLOSE_BRACE
                      ;
/************************Fin producciones para parametros de solicitud ModificarUsuario************************/       



/**************************Producciones para parametros de solicitud EliminarUsuario*************************/
startParamsDeleteUser ::= OPEN_BRACE paramDeleteUser:p endParamsDeleteUser
                        {:
                            parametros.add(p);
                            solicitudes.add(new Solicitud(TipoSolicitud.DELETE_USER, parametros));
                            solicitudes.forEach(s -> {
                                System.out.println("Solicitud tipo " + s.getTipo());
                                s.getParametros().forEach(param -> System.out.println(param.getName() + " - " + param.getValue()));
                            });
                        :}
                        ;

paramDeleteUser ::= QUOTE_MARK PARAM_USUARIO:p QUOTE_MARK COLON QUOTE_MARK value:v QUOTE_MARK
                    {:
                        RESULT = new Parametro(p.getLexema(), v);
                    :}
                    ;

endParamsDeleteUser ::= CLOSE_BRACE COMMA startParamsDeleteUser         {:parametros.clear();:}
                      | CLOSE_BRACE
                      ;
/************************Fin producciones para parametros de solicitud EliminarUsuario***********************/

value ::= VALUE:v           {:RESULT = v.getLexema();:}
        ;

endContent ::= CLOSE_BRACKET CLOSE_BRACE;

endRequest ::= LESS_THAN END_REQUEST EXCLAMATION_MARK GREATER_THAN;